# Maintainer: Daniel Bermond <dbermond@archlinux.org>
_name=libmysofa
pkgname=lib32-libmysofa
pkgver=1.3.2
pkgrel=1
pkgdesc='C library to read HRTFs if they are stored in the AES69-2015 SOFA format (32-bit)'
arch=('x86_64')
url='https://github.com/hoene/libmysofa/'
license=('BSD')
depends=('lib32-zlib')
makedepends=('cmake' 'lib32-cunit')
checkdepends=('nodejs')
provides=('libmysofa.so')
source=("https://github.com/hoene/libmysofa/archive/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha256sums=('6c5224562895977e87698a64cb7031361803d136057bba35ed4979b69ab4ba76')

build() {
    export CC="armv7h-linux-gnueabihf-gcc"
    export CXX="armv7h-linux-gnueabihf-g++"
    export PKG_CONFIG="armv7h-linux-gnueabihf-pkg-config"
    cmake \
        -G 'Ninja' \
        -B "${_name}-${pkgver}/build" \
        -S "${_name}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_INSTALL_LIBDIR:PATH='/usr/lib32' \
        -DCMAKE_INSTALL_INCLUDEDIR:PATH='/usr/include32' \
        -DCODE_COVERAGE:BOOL='OFF' \
        -Wno-dev
    sed -i 's|-I/usr/include|-I/usr/include32|' "${_name}-${pkgver}/build/build.ninja"
    cmake --build "${_name}-${pkgver}/build"

}

check() {
    ctest --test-dir "${_name}-${pkgver}/build" --output-on-failure --stop-on-failure
}

package() {
    DESTDIR="${pkgdir}" cmake --install "${_name}-${pkgver}/build"
    rm -r "${pkgdir}/usr/"{share,bin}
    install -D -m644 "${_name}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
