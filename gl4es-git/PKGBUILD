# Maintainer: Michał Wojdyła < micwoj9292 at gmail dot com >

pkgname=gl4es-git
pkgver=1.1.4
pkgrel=1
pkgdesc='OpenGL 2.1/1.5 to GL ES 2.0/1.1 translation library'
arch=('armv7h' 'x86_64')
url='https://github.com/ptitSeb/gl4es'
license=('MIT')
depends=('glibc' 'libx11')
makedepends=('cmake')
provides=('gl4es')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd gl4es
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'

}

build() {
  cd gl4es
  cmake -B build -S . \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  cd gl4es/build
  make DESTDIR=${pkgdir} install
  install -Dm644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
