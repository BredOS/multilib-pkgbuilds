# Maintainer: Panda <panda@bredos.org>
# Build order: arm32-binutils -> arm32-linux-api-headers -> lib32-glibc -> arm32-gcc -> lib32-glibc (again)

_kernel_arch=arm
pkgname=arm32-linux-api-headers
pkgver=6.9
pkgrel=1
pkgdesc="Kernel headers sanitized for use in armv7h userspace"
arch=(any)
url='https://www.kernel.org'
license=(GPL2)
makedepends=(rsync)
options=(!strip)
source=(https://www.kernel.org/pub/linux/kernel/v6.x/linux-$pkgver.tar.{xz,sign})
sha256sums=('24fa01fb989c7a3e28453f117799168713766e119c5381dac30115f18f268149'
            'SKIP')
validpgpkeys=(
  'ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linus Torvalds
  '647F28654894E3BD457199BE38DBBDC86092693E'  # Greg Kroah-Hartman
)


build() {
  cd linux-$pkgver

  make ARCH=$_kernel_arch mrproper
}

package() {
  cd linux-$pkgver
  make INSTALL_HDR_PATH="$pkgdir/usr/" ARCH=$_kernel_arch V=0 headers_install
  mv "$pkgdir/usr/include" "$pkgdir/usr/include32"

  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -or -name ..install.cmd \) -delete
}
