# Maintainer: Faruk Dikcizgi <boogiepop@gmx.de>
# Contributor: Jan Keith Darunday <jkcdarunday+aur.archlinux.org@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Dan Johansen <strit@manjaro.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Disabled all artifacts except panfrost since they cause color issues on runtime 
#  - Removed Gallium3D drivers/packages for chipsets that don't exist in our ARM devices (intel, VMware svga).
#  - added broadcom and panfrost vulkan packages
#  - enable lto for aarch64

pkgname=lib32-mesa-panfork-git
pkgdesc="Libgl & gbm with panfrost for Mali G610 from icecream95 (32-bit)"
pkgver=r164486.2e8aead0016
pkgrel=1
arch=('aarch64')
makedepends=(
  lib32-clang
  lib32-expat
  lib32-gcc-libs
  lib32-glibc
  lib32-libdrm
  lib32-libelf
  lib32-libglvnd
  lib32-libx11
  lib32-libxcb
  lib32-libxext
  lib32-libxfixes
  lib32-libxml2
  lib32-libxrandr
  lib32-libxshmfence
  lib32-libxxf86vm
  lib32-llvm
  lib32-llvm-libs
  lib32-lm_sensors
  lib32-systemd
  lib32-vulkan-icd-loader
  lib32-wayland
  lib32-xcb-util-keysyms
  lib32-zlib
  lib32-zstd
  meson
  python-mako
  python-packaging
  bison
  flex
  cmake
  libxfixes
  libxxf86vm
  lm_sensors
  libxshmfence
  wayland-protocols
  libdrm
  libxrandr
  libglvnd
)
url="https://gitlab.com/hbiyik/mesa"
license=('custom')
options=('!lto')
source=("mesa::git+https://github.com/hbiyik/mesa.git#branch=panfork"
        LICENSE)
sha512sums=('SKIP'
            'f9f0d0ccf166fe6cb684478b6f1e1ab1f2850431c06aa041738563eb1808a004e52cdec823c103c9e180f03ffc083e95974d291353f0220fe52ae6d4897fecc7')

validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D'  # Emil Velikov <emil.l.velikov@gmail.com>
              '946D09B5E4C9845E63075FF1D961C596A7203456'  # Andres Gomez <tanty@igalia.com>
              'E3E8F480C52ADD73B278EE78E1ECBE07D7D70895'  # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>
              'A5CC9FEC93F2F837CB044912336909B6B25FADFA'  # Juan A. Suarez Romero <jasuarez@igalia.com>
              '71C4B75620BC75708B4BDB254C95FAAB3EB073EC'  # Dylan Baker <dylan@pnwbakers.com>
              '57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>

pkgver() {
  cd mesa
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  arch-meson mesa build \
    --cross-file arm-lib32 \
    -Dgallium-drivers=panfrost \
    -Dvulkan-drivers= \
    -Ddri3=enabled \
    -Degl=enabled \
    -Dllvm=disabled \
    -Dmicrosoft-clc=disabled \
    -Dintel-clc=disabled \
    -Dplatforms=wayland,x11 \
    -Dgallium-nine=true \
    -Dgbm=enabled \
    -Dgles1=disabled \
    -Dgles2=enabled \
    -Dglx=dri \
    -Dshared-glapi=enabled \
    -Dglvnd=true

  ninja -C build
  meson compile -C build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  DESTDIR="${srcdir}/fakeinstall" meson install -C build
}

package() {
 depends=(
    lib32-expat
    lib32-gcc-libs
    lib32-glibc
    lib32-libdrm
    lib32-libelf
    lib32-libglvnd
    lib32-libx11
    lib32-libxcb
    lib32-libxext
    lib32-libxfixes
    lib32-libxshmfence
    lib32-libxxf86vm
    lib32-llvm-libs
    lib32-lm_sensors
    lib32-wayland
    lib32-zlib
    lib32-zstd

    mesa-panfork-git
  )
  provides=('lib32-mesa-libgl' 'lib32-opengl-driver' 'lib32-mesa')
  conflicts=('lib32-mesa-libgl' 'lib32-mesa')

  #install libs
  _install fakeinstall/usr/lib32/dri/*_dri.so
  _install fakeinstall/usr/lib32/lib{gbm,glapi}.so*
  _install fakeinstall/usr/lib32/libGL*
  _install fakeinstall/usr/lib32/libEG*
  _install fakeinstall/usr/include32
  _install fakeinstall/usr/lib32/pkgconfig

  _install fakeinstall/usr/lib32/d3d
  
  find fakeinstall -depth -print0 | xargs -0 rm -rf

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}
