# Maintainer: Panda <panda@bredos.org>
# Build order: arm32-binutils -> arm32-linux-api-headers -> lib32-glibc -> arm32-gcc -> lib32-glibc (again)

_target=armv7h-linux-gnueabihf
pkgname=lib32-glibc
pkgver=2.39
pkgrel=1
_commit=ef321e23c20eebc6d6fb4044425c00e6df27b05f
pkgdesc="GNU C Library for armv7h"
arch=(any)
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
depends=()
makedepends=(git arm32-binutils arm32-gcc arm32-linux-api-headers arm32-filesystem python)
options=(!buildflags !strip staticlibs)
source=(git+https://sourceware.org/git/glibc.git#commit=${_commit})

validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8  # "Carlos O'Donell <carlos@systemhalted.org>"
              BC7C7372637EC10C57D7AA6579C43DFBF1CF2187) # Siddhesh Poyarekar
b2sums=('SKIP')

prepare() {
  mkdir -p glibc-build
}

build() {
  cd glibc-build
  echo "build-programs=no" > configparms
  echo "cross-compiling=yes" >> configparms
  echo 'slibdir=/lib32' >> configparms
  echo 'rtlddir=/lib32' >> configparms
  echo 'sbindir=/bin' >> configparms
  echo 'rootsbindir=/bin' >> configparms

  # remove hardening options for building libraries
  #export CFLAGS="-U_FORTIFY_SOURCE -mlittle-endian -O2"
  #export CPPFLAGS="-U_FORTIFY_SOURCE -O2"
  unset LD_LIBRARY_PATH

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

unset CFLAGS CXXFLAGS
#Use CFLAGS/CXXFLAGS from Arch Linux ARM
CFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=neon -O2 -pipe -fstack-protector-strong -fno-plt -fexceptions \
-Wp,-D_FORTIFY_SOURCE=3 -Wformat -Werror=format-security \
-fstack-clash-protection \
-fno-omit-frame-pointer -mno-omit-leaf-frame-pointer"
CXXFLAGS="$CFLAGS -Wp,-D_GLIBCXX_ASSERTIONS"

  ../glibc/configure \
      --prefix=/usr \
      --host=$_target \
      --libdir=/lib32 \
      --libexecdir=/usr/lib32 \
      --includedir=/usr/include32 \
      --with-bugurl=https://github.com/BredOS/multilib-pkgbuilds/issues/ \
      --enable-kernel=6.6 \
      --enable-bind-now \
      --disable-multi-arch \
      --enable-stack-protector=strong \
      --disable-profile \
      --disable-werror \
      --disable-timezone-tools 

  make -j$(nproc)
}

package() {
  cd glibc-build
  make DESTDIR="$pkgdir" install 
  
  #we don't want static libraries. Only keep the one that we really need.
  find "$pkgdir"/usr/  -name '*.a' -and -not -name libc_nonshared.a -delete
  
  #copy everything from pkgdir/lib and move it to pkgdir/usr/lib32
  mv -v "$pkgdir"/lib32/ "$pkgdir"/usr/lib32/
  rm -rvf "$pkgdir"/usr/{bin,share}
  rm -rvf "$pkgdir"/{lib,var,etc}
  mkdir -p "$pkgdir"/usr/lib/
  # fix broken symlinks

  # link the dynamic linker
  ln -sv ../lib32/ld-linux-armhf.so.3 "$pkgdir"/usr/lib/ld-linux-armhf.so.3

  #strip manually
  find "$pkgdir"/usr/ -name '*.so' -and ! -name 'libc.so' -print0 | xargs -0 $_target-strip --strip-all

  #create symlink to the shared library since we don't have the static one (gcc needs this)
  ln -sv libpthread.so.0 "$pkgdir"/usr/lib32/libpthread.so
}
