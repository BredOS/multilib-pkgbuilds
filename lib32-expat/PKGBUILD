# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-expat
_name=${pkgname#lib32-}
pkgver=2.6.4
pkgrel=1
pkgdesc='An XML Parser library'
arch=(x86_64)
url=https://libexpat.github.io/
_url="https://github.com/libexpat/libexpat"
license=(MIT)
depends=(
  "expat=$pkgver"
  lib32-glibc
)
makedepends=(
  cmake
  git
)
provides=(libexpat.so)
source=($_name::git+$_url?signed#tag=R_${pkgver//./_})
sha512sums=('99a4d3014a43b5dc5862f18daf901656ff2698f42bf9080f7b46c995fb7f69126add770e9ce52d26be44432852b9aa5ff326a75eb880d6921a3a3ae822792ab6')
b2sums=('063dcf84994afa7cd9bc4a172a328c1b13335bd2fd6456b9727e42e1821898e000a152a2991334924ded1813d4b59a5f1237d80dd37df89b46fb41124d40bc25')
validpgpkeys=(3176EF7DB2367F1FCA4F306B1F9B0E909AF37285) # Sebastian Pipping

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib32
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_INCLUDEDIR=include32
    -S $_name/$_name
    -W no-dev
  )

  export CC="armv7h-linux-gnueabihf-gcc"
  export CXX="armv7h-linux-gnueabihf-g++"
  export PKG_CONFIG="armv7h-linux-gnueabihf-pkg-config"

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name/$_name/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  rm -rf "$pkgdir"/usr/{bin,share/man,share/doc}
}

# vim:set sw=2 sts=-1 et:
