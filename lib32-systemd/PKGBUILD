# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib32-systemd
_pkgbasename=systemd
_tag='256.8'
# Upstream versioning is incompatible with pacman's version comparisons, one
# way or another. So we replace dashes and tildes with the empty string to
# make sure pacman's version comparing does the right thing for rc versions:
pkgver="${_tag/[-~]/}"
pkgrel=1
pkgdesc='system and service manager (32-bit)'
arch=('x86_64')
url='https://www.github.com/systemd/systemd'
license=('LGPL-2.1-or-later')
depends=('systemd-libs' 'lib32-gcc-libs' 'lib32-libcap' 'lib32-libgcrypt'
         'lib32-libxcrypt' 'lib32-xz' 'lib32-zstd')
makedepends=('git' 'gperf' 'intltool' 'lib32-acl' 'lib32-bzip2'
             'lib32-curl' 'lib32-dbus' 'lib32-gcc-libs' 'lib32-glib2'
             'lib32-gnutls' 'lib32-libelf' 'lib32-libidn2' 'lib32-pcre2'
             'libxslt' 'meson' 'python-jinja')
checkdepends=('systemd')
provides=('libsystemd.so' 'libudev.so')
validpgpkeys=('63CDA1E5D3FC22B998D20DD6327F26951A015CC4'  # Lennart Poettering <lennart@poettering.net>
              'A9EA9081724FFAE0484C35A1A81CEA22BC8C7E2E'  # Luca Boccassi <luca.boccassi@gmail.com>
              '9A774DB5DB996C154EBBFBFDA0099A18E29326E1'  # Yu Watanabe <watanabe.yu+github@gmail.com>
              '5C251B5FC54EB2F80F407AAAC54CA336CFEB557E') # Zbigniew JÄ™drzejewski-Szmek <zbyszek@in.waw.pl>
source=("git+https://github.com/systemd/systemd#tag=v${_tag}?signed")
sha512sums=('4ffda9aee045ac793e87a86cdee1d089960542e21b44993381095399baacdf79c1867d36cdc242767acee725603ea70f942f8fd61e0a69fe0c888b0e4dd17a37')

_backports=(
)

_reverts=(
)

prepare() {
  cd "${_pkgbasename}"

  local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done
}

build() {
  export CC="armv7h-linux-gnueabihf-gcc"
  export CXX="armv7h-linux-gnueabihf-g++"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="-Wno-redundant-decls"
  export CXXFLAGS="-Wno-redundant-decls"

  local _timeservers=({0..3}.arch.pool.ntp.org)
  local _nameservers=(
    # We use these public name services, ordered by their privacy policy (hopefully):
    #  * Cloudflare (https://1.1.1.1/)
    #  * Quad9 (https://www.quad9.net/)
    #  * Google (https://developers.google.com/speed/public-dns/)
    '1.1.1.1#cloudflare-dns.com'
    '9.9.9.9#dns.quad9.net'
    '8.8.8.8#dns.google'
    '2606:4700:4700::1111#cloudflare-dns.com'
    '2620:fe::9#dns.quad9.net'
    '2001:4860:4860::8888#dns.google'
  )
 
  local _meson_options=(
    --libexecdir	/usr/lib32
    --libdir		/usr/lib32
    --cross-file arm-lib32

    -Dversion-tag="${_meson_version}-arch"
    -Dvcs-tag=false
    -Dshared-lib-tag="${_meson_version}"
    -Dmode=release

    # components & features
    -Dapparmor=disabled
    -Daudit=disabled
    -Dbacklight=false
    -Dbinfmt=false
    -Dblkid=disabled
    -Dbootloader=disabled
    -Dbpf-framework=disabled
    -Dcoredump=false
    -Dcreate-log-dirs=false
    -Defi=false
    -Denvironment-d=false
    -Dfirstboot=false
    -Dhibernate=false
    -Dhomed=disabled
    -Dhostnamed=false
    -Dhtml=disabled
    -Dhwdb=false
    -Dima=false
    -Dimportd=disabled
    -Dkmod=disabled
    -Dldconfig=false
    -Dlibarchive=disabled
    -Dlibcryptsetup=disabled
    -Dlibcryptsetup-plugins=disabled
    -Dlibfido2=disabled
    -Dlibidn2=enabled
    -Dlibiptc=disabled
    -Dlocaled=false
    -Dlogind=false
    -Dlz4=disabled
    -Dmachined=true
    -Dman=disabled
    -Dmicrohttpd=disabled
    -Dnetworkd=false
    -Dnss-myhostname=true
    -Dnss-mymachines=enabled
    -Dnss-resolve=enabled
    -Dnss-systemd=true
    -Doomd=false
    -Dpam=disabled
    -Dpasswdqc=disabled
    -Dportabled=false
    -Dpstore=false
    -Dpwquality=disabled
    -Dqrencode=disabled
    -Dquotacheck=false
    -Drandomseed=false
    -Dremote=disabled
    -Drepart=disabled
    -Dresolve=true
    -Drfkill=false
    -Dseccomp=disabled
    -Dselinux=disabled
    -Dsysext=false
    -Dsysusers=false
    -Dtimedated=false
    -Dtimesyncd=false
    -Dtmpfiles=false
    -Dtpm2=disabled
    -Dtpm=false
    -Dtranslations=false
    -Dukify=disabled
    -Duserdb=false
    -Dutmp=false
    -Dvconsole=false
    -Dxdg-autostart=false
    -Dxenctrl=disabled
    -Dxkbcommon=disabled

    -Ddbuspolicydir=/usr/share/dbus-1/system.d
    -Ddefault-kill-user-processes=false
    -Ddefault-locale='C.UTF-8'
    -Dfallback-hostname='archlinux'
    -Dnologin-path=/usr/bin/nologin
    -Dntp-servers="${_timeservers[*]}"
    -Ddns-servers="${_nameservers[*]}"
    -Drpmmacrosdir=no
    -Dsysvinit-path=
    -Dsysvrcnd-path=
  )

  arch-meson "${_pkgbasename}" build "${_meson_options[@]}"

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,lib32/systemd,share}
}
