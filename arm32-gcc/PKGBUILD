# Maintainer: Panda <panda@bredos.org>
# Build order: arm32-binutils -> arm32-linux-api-headers -> lib32-glibc -> arm32-gcc -> lib32-glibc (again)

_target=armv7h-linux-gnueabihf
pkgname=arm32-gcc
pkgver=14.1.0
pkgrel=2
pkgdesc='The GNU Compiler Collection - cross compiler for armv7h'
arch=(aarch64)
url='https://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=(libmpc zstd libisl arm32-filesystem lib32-glibc arm32-binutils)
makedepends=(arm32-binutils git arm32-linux-api-headers)
provides=(gcc-multilib arm32-gcc lib32-gcc-libs arm32-gcc-fortran arm32-gcc-objc arm32-gcc-lto)
conflicts=(arm32-gcc)
options=(!emptydirs !strip staticlibs)
# https://gcc.gnu.org/git/gitweb.cgi?p=gcc.git
_commit=cd0059a1976303638cea95f216de129334fc04d1
source=(git+https://sourceware.org/git/gcc.git#commit=${_commit}
        c89
        c99)

sha256sums=('SKIP'
            'f65853014cf7c640656e4bf05996eecd29085866a9eb031deb750c580e42744d'
            '56a5ee0db3a41e47c9900cff3a99a605a2ccd0880b3e49f383e0dab0163772c8')

validpgpkeys=('F3691687D867B81B51CE07D9BBE43771487328A9'  # bpiotrowski@archlinux.org
              '86CFFCA918CF3AF47147588051E8B148A9999C34'  # evangelos@foutrelis.com
              '13975A70E63C361C73AE69EF6EEB81F8981C74C7'  # richard.guenther@gmail.com
              'D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62') # Jakub Jelinek <jakub@redhat.com>

prepare() {
  cd $_basedir

  rm -rf "$srcdir"/gcc-build
  mkdir "$srcdir"/gcc-build
}

build() {
  cd gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}
  # Credits @allanmcrae
  # https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/gcc/PKGBUILD
  # TODO: properly deal with the build issues resulting from this
  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  "$srcdir"/gcc/configure \
      --prefix=/usr \
      --libexecdir=/usr/lib32 \
      --with-sysroot=/usr/$_target/sys-root \
      --target=$_target \
      --with-bugurl=https://github.com/BredOS/multilib-pkgbuilds/issues \
      --disable-nls --enable-default-pie \
      --enable-languages=c,c++,fortran,lto,objc,obj-c++ \
      --with-system-zlib --enable-__cxa_atexit \
      --enable-linker-build-id \
      --enable-plugin \
      --with-linker-hash-style=gnu --enable-gnu-indirect-function \
      --disable-multilib --disable-werror \
      --enable-checking=release --enable-host-shared --enable-default-ssp --disable-libstdcxx-pch \
      --with-arch=armv7-a --with-float=hard --with-fpu=neon

  make -j14
}

package() {
  cd gcc-build

  make install-strip DESTDIR="$pkgdir"
  ln -s $_target-gcc "$pkgdir"/usr/bin/$_target-cc

  mv -v "$pkgdir"/usr/$_target/lib/* "$pkgdir"/usr/lib32/
  mv -v "$pkgdir"/usr/$_target/include/ "$pkgdir"/usr/include32/
  cp -rv "$pkgdir"/usr/lib/gcc/* "$pkgdir"/usr/lib32/gcc/
  rm -rf "$pkgdir"/usr/lib/gcc/$_target
  ln -s /usr/lib32/gcc/$_target "$pkgdir"/usr/lib/gcc/$_target

  # POSIX conformance launcher scripts for c89 and c99
  install -Dm755 "$srcdir/c89" "$pkgdir/usr/bin/$_target-c89"
  install -Dm755 "$srcdir/c99" "$pkgdir/usr/bin/$_target-c99"

  # Remove files that conflict with host gcc package
  rm -rf "$pkgdir"/usr/{include,lib/libcc*,share}

}
