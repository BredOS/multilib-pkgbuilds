# Maintainer: Bart De Vries <bart at mogwai dot be>

_pkgname=box64
pkgname=${_pkgname}-rk3588-git
pkgver=0.2.6.r74.g3454afb3
pkgrel=1
pkgdesc='Linux Userspace x86_64 Emulator with a twist'
arch=('aarch64' 'riscv64')
url='https://github.com/ptitSeb/box64'
license=('MIT')
install="box64-git.install"
depends=('gcc-libs')
#optdepends=('')
makedepends=('git' 'cmake' 'python')
provides=(box64 box64-rk3588)
conflicts=(box64 box64-rk3588)
source=("git+https://github.com/ptitSeb/box64.git#branch=main"
        "box64-git.install")
sha256sums=('SKIP'
            '7e94518dbd11121f150a51b64f4c0ec11f844a83f7b15205d28c1de63de699f2')

pkgver() {
    cd ${_pkgname}
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd ${_pkgname}
    cmake -B build -S . \
          -DARM_DYNAREC=ON \
          -DRK3588=1 \
          -DCMAKE_BUILD_TYPE=RelWithDebInfo \
          -DCMAKE_INSTALL_PREFIX=/usr
    make -C build
}

package() {
    cd ${_pkgname}/build
    make DESTDIR=${pkgdir} install

    install -Dm644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"

    # Install documentation
    install -d "${pkgdir}/usr/share/doc/${_pkgname}/"
    cp -R ../docs/* "${pkgdir}/usr/share/doc/${_pkgname}/"

}
